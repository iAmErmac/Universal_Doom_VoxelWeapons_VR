ACTOR Vanilla_Shotgun : ZWeapon
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Inventory.PickupMessage "$GOTSHOTGUN"
	Obituary "$OB_MPSHOTGUN"
	Tag "$TAG_SHOTGUN"
	
	States
	{
	Ready:
		SHTG A 0 A_ReplaceSprite("vvw_models","SHTA,SHD4")
		"####" A 1 A_WeaponReady
		Loop
	Deselect:
		"####" "#" 0 A_JumpIf(GetCVAR("vvw_fastswitch")==0, 2)
		"####" A 1 A_Lower(60)
		Wait
		"####" A 1 A_Lower
		Wait
	Select:
		SHTG A 0 A_ReplaceSprite("vvw_models","SHTA,SHD4")
		"####" "#" 0 A_JumpIf(GetCVAR("vvw_fastswitch")==0, 2)
		"####" A 1 A_Raise(60)
		Wait
		"####" A 1 A_Raise
		Wait
	Fire:
		"####" A 3
		"####" "#" 0 A_JumpIf(GetCVAR("vvw_weapons")==1, "Fire.Enhanced")
		"####" "#" 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
		"####" "#" 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
		"####" A 7 A_GunFlash
		"####" BC 5
		"####" D 4
		"####" CB 5
		"####" A 3
		"####" A 7 A_ReFire
		Goto Ready
	Fire.Enhanced:
		"####" "#" 0 bright A_FireBullets (5.6, 0, 7, 5, "EnShotPuff")
		"####" "#" 0 A_PlaySound ("weapons/shotgun/fire", CHAN_WEAPON)
		"####" A 7 A_GunFlash
		"####" A 0 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		"####" B 5
		"####" "#" 0 A_PlaySound ("weapons/shotgun/pump", CHAN_WEAPON)
		"####" C 5
		"####" D 4
		"####" CB 5
		"####" A 3
		"####" A 7 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_tracers")==false, "Flash.Vanilla")
		TNT1 AAAAAA 0 A_FireProjectile("BulletTracer",random(-3,3),0,0,0,FPF_NOAUTOAIM,random(-1,1))
	Flash.Vanilla:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_flash_sprite")==false, "NoFlashSprite")
		SHTF A 4 Bright A_Light1
		SHTF B 3 Bright A_Light2
		Goto LightDone
	NoFlashSprite:
		TNT1 A 4 A_Light1
		TNT1 A 3 A_Light2
		Goto LightDone
	Spawn:
		SHOT A 0 A_CheckSpawnModel()
		SHOT A 0 A_ReplaceSprite("vvw_pickupmodel","VSHT", true, 1)
		"####" A 1
		Loop
	Spawn2:
		VSGS A 0 A_ReplaceSprite("vvw_pickupmodel","VSG4", true, 1)
		"####" A 1
		Goto Spawn
	Dummy:
		SHTA ABCD 0
		SHD4 ABCD 0
		VSHT A 0
		VSG4 A 0
		Stop
	}
}