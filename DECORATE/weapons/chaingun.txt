ACTOR Vanilla_Chaingun : ZWeapon
{
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "$GOTCHAINGUN" // "You got the chaingun"
	Obituary "$OB_MPCHAINGUN" // "%o was mowed down by %k's chaingun."
	Tag "$TAG_CHAINGUN"
	
	States
	{
	Ready:
		CHGG A 0 A_ReplaceSprite("vvw_models","CHGA,CGD4")
		"####" A 1 A_WeaponReady
		Loop
	Deselect:
		"####" "#" 0 A_JumpIf(GetCVAR("vvw_fastswitch")==0, 2)
		"####" A 1 A_Lower(60)
		Wait
		"####" A 1 A_Lower
		Wait
	Select:
		CHGG A 0 A_ReplaceSprite("vvw_models","CHGA,CGD4")
		"####" "#" 0 A_JumpIf(GetCVAR("vvw_fastswitch")==0, 2)
		"####" A 1 A_Raise(60)
		Wait
		"####" A 1 A_Raise
		Wait
	Fire:
		"####" A 0 A_JumpIf(GetCVAR("vvw_weapons")==1, "Fire.Enhanced")
		"####" "#" 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		"####" "#" 0 A_GunFlash
		"####" A 4 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		"####" "#" 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		"####" "#" 0 A_GunFlash("Flash2")
		"####" B 4 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
		"####" "#" 0 A_ReFire
		Goto Ready
	Fire.Enhanced:
		"####" A 0 A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON)
		"####" "#" 0 A_GunFlash
		"####" A 4 A_FireBullets(5.6, 0, 1, 5, "EnBulletPuff")
		"####" "#" 0 A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON)
		"####" "#" 0 A_GunFlash("Flash2")
		"####" B 4 A_FireBullets(5.6, 0, 1, 5, "EnBulletPuff")
		"####" "#" 0 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_tracers")==false, "Flash.Vanilla")
		TNT1 A 0 A_FireProjectile("BulletTracer",random(-1,1),0,0,0,FPF_NOAUTOAIM,random(-1,1))
		Goto Flash.Vanilla
	Flash2:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_tracers")==false, "Flash2.Vanilla")
		TNT1 A 0 A_FireProjectile("BulletTracer",random(-1,1),0,0,0,FPF_NOAUTOAIM,random(-1,1))
		Goto Flash2.Vanilla
	Flash.Vanilla:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_flash_sprite")==false, "NoFlashSprite")
		CHGF A 4 Bright A_Light1
		Goto LightDone
	Flash2.Vanilla:
		TNT1 A 0 A_JumpIf(GetCVAR("vvw_flash_sprite")==false, "NoFlashSprite")
		CHGF B 4 Bright A_Light2
		Goto LightDone
	NoFlashSprite:
		TNT1 A 4 A_Light1
		Goto LightDone
	Spawn:
		MGUN A 0 A_CheckSpawnModel()
		MGUN A 0 A_ReplaceSprite("vvw_pickupmodel","VMGN", true, 1)
		"####" A 1
		Loop
	Spawn2:
		VCGS A 0 A_ReplaceSprite("vvw_pickupmodel","VCG4", true, 1)
		"####" A 1
		Goto Spawn
	Dummy:
		CHGA AB 0
		CGD4 AB 0
		VMGN A 0
		VCG4 A 0
		Stop
	}
}